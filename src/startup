#! /bin/csh -f
# Written by Furey.
# With additions from Tony.
# With changes from Kahn.

# Set the port number.
set port = 9500
if ( "$1" != "" ) set port="$1"

# Change to area directory.
cd ../area

# Set limits.
nohup
limit stack 1024k
if ( -e SHUTDOWN.TXT ) rm -f SHUTDOWN.TXT

while ( 1 )
    # If you want to have logs in a different directory,
    #   change the 'set logfile' line to reflect the directory name.
    set index = 1000
    while ( 1 )
	set logfile = ../log/$index.log
	if ( ! -e $logfile ) break
	@ index++
    end

    # Update to new code if possible.
    if ( -e ../src/envy.new ) then
	\mv ../src/envy2 ../src/envy.old
	\mv ../src/envy.new ../src/envy2
    endif

    # Run envy.
    # Check if already running
    set matches = `netstat -a | grep ":$port " | grep -c LISTEN`
    if ( $matches >= 1 ) then
        # Already running
        echo MUD Already running.
        exit 0
    endif
    ../src/envy $port >&! $logfile

    if ( -e core ) then
	\mv core ../src/
    endif

    # Restart, giving old connections a chance to die.
    if ( -e SHUTDOWN.TXT ) then
	rm -f SHUTDOWN.TXT
	exit 0
    endif

    sleep 5
end
